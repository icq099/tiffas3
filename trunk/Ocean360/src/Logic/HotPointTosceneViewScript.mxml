<?xml version="1.0" encoding="utf-8"?>
<HotPointTosceneView creationComplete="init()" xmlns="myviews.*" xmlns:mx="http://www.adobe.com/2006/mxml" width="400" height="300">
	<mx:Script>
		<![CDATA[
			import flash.net.navigateToURL;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.core.Application;
			import mx.managers.PopUpManager;
			import myviews.AddHotPointToScenePop;
			import model.PopHotPoint;
			import mx.events.ListEvent;
			import model.MXml;
			//记录选中场景所有热点的xmllist;
			private var hotpoint_xml:XMLList;
			private var _pop_add_window:AddHotPointToScenePop;
			
			//点击场景列表事件
			private function onSceneItemClick(e:ListEvent):void{
				upDataHotpointList();
			}
			private function upDataHotpointList():void{
				var xml_list:XMLList=MXml.getInstance().xml_menu.Scene.(@sceneID==list_scene.selectedIndex).sample;
				hotpoint_xml=new XMLList();
				for(var i:int=0;i<xml_list.length();i++){
					hotpoint_xml[i]=MXml.getInstance().getHotPointXml(xml_list[i]);
				}
				list_hotpoint.dataProvider=hotpoint_xml.@name;
			}
			//双击展项列表事件
			private function onHotPointItemClick(e:ListEvent):void{
				PopHotPoint.popUp(hotpoint_xml[list_hotpoint.selectedIndex]);
			}
			//添加按钮点击事件
			private function onAddBtnClick(e:Event):void{
				PopUpManager.addPopUp(pop_add_window,DisplayObject(Application.application),true);
				PopUpManager.centerPopUp(pop_add_window);
			};
			private function onDeleteClick(e:Event):void{
				if(operatable){
					Alert.show("确定删除？","提示",Alert.YES|Alert.NO,null,onAlerClose);
				}
			}
			private function onAlerClose(e:CloseEvent):void{
				if(e.detail==Alert.YES){
					MXml.getInstance().deleteMenuXml(list_scene.selectedIndex,list_hotpoint.selectedIndex);
					upDataHotpointList();
				}
			}
			//弹出窗口关闭事件
			private function onPopClose(e:Event):void{
				PopUpManager.removePopUp(pop_add_window);
			}
			//得到弹出窗口实例
			private function get pop_add_window():AddHotPointToScenePop{
				if(_pop_add_window==null){
					_pop_add_window=new AddHotPointToScenePopScript();
					_pop_add_window.addEventListener(CloseEvent.CLOSE,onPopClose);
					_pop_add_window.addEventListener(FlexEvent.CREATION_COMPLETE,function(e:Event):void{
						_pop_add_window.btn_add.addEventListener(MouseEvent.CLICK,onPopComfirm);
						_pop_add_window.list_hotpoint.doubleClickEnabled=true;
						_pop_add_window.addEventListener(MouseEvent.DOUBLE_CLICK,onPopComfirm);
					});
				}
				return _pop_add_window;
			}
			//点击弹出窗口的确定按钮
			private function onPopComfirm(e:Event):void{
				if((list_scene.selectedIndex!=-1)&&(_pop_add_window.list_hotpoint.selectedIndex!=-1)){
					MXml.getInstance().addMenuXml(list_scene.selectedIndex,MXml.getInstance().getHotPointXmlByIndex(_pop_add_window.list_hotpoint.selectedIndex).@id);
					upDataHotpointList();
				}
			}
			private function onSceneDClick(e:Event):void{
				navigateToURL(new URLRequest("pv360flex.html#scene="+list_scene.selectedIndex),"_blank");
			}
			private function get operatable():Boolean{
				return (list_hotpoint.selectedIndex!=-1)&&(list_scene.selectedIndex!=-1);
			}
			private function init():void{
				list_hotpoint.doubleClickEnabled=true;
				list_scene.doubleClickEnabled=true;
				list_scene.addEventListener(ListEvent.CHANGE,onSceneItemClick);
				list_scene.addEventListener(MouseEvent.DOUBLE_CLICK,onSceneDClick);
				list_hotpoint.addEventListener(ListEvent.ITEM_DOUBLE_CLICK,onHotPointItemClick);
				btn_add.addEventListener(MouseEvent.CLICK,onAddBtnClick);
				btn_delete.addEventListener(MouseEvent.CLICK,onDeleteClick);
			}
		]]>
	</mx:Script>
	<mx:Binding source="MXml.getInstance().xml_scene.Travel.Scene.@name" destination="list_scene.dataProvider">
	</mx:Binding>
	<mx:Binding source="MXml.getInstance().isComplete" destination="btn_add.enabled">
	</mx:Binding>
	<mx:Binding source="MXml.getInstance().isComplete" destination="btn_delete.enabled">
	</mx:Binding>
</HotPointTosceneView>
